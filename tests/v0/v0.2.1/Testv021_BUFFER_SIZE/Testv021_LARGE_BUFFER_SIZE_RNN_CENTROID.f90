program Testv021_LARGE_BUFFER_SIZE_RNN_CENTROID
    use KdTreeFortran
    use iso_fortran_env, only: real64
    implicit none

    call largeBufferSize_rNN_Centroid()
    contains

        !> 1000 random 3-D points; 1000 queries with random centroids.
        !! Result count is verified against a brute-force euclidean scan each iteration.
        subroutine largeBufferSize_rNN_Centroid()
            integer, parameter         :: N = 1000, NDIM = 3, NITER = 1000
            type(KdTree)                 :: t
            real(real64)               :: coords(NDIM, N), centroid(NDIM)
            type(KdNodePtr), allocatable :: res(:)
            real(real64)               :: d, r
            integer                    :: i, j, expected

            call random_number(coords)
            coords = coords * 100.0_real64
            call t%build(coords)

            r = 20.0_real64

            do i = 1, NITER
                call random_number(centroid)
                centroid = centroid * 100.0_real64

                res = t%rNN_Centroid(centroid, r, bufferSize=1000000)

                expected = 0
                do j = 1, N
                    d = sqrt(sum((centroid - coords(:, j))**2))
                    if (d .le. r) expected = expected + 1
                end do

                if (size(res) .ne. expected) then
                    write(*, '(A)') '--- largeBufferSize_rNN_Centroid ---'
                    write(*, *) 'iteration:', i, '  expected:', expected, '  got:', size(res)
                    stop 1
                end if
            end do

        end subroutine largeBufferSize_rNN_Centroid

end program Testv021_LARGE_BUFFER_SIZE_RNN_CENTROID
